import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'     // ButterKnife (https://github.com/JakeWharton/butterknife)
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.vocketlist.android"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            // 컴파일 시간을 줄이기 위해서...
            minSdkVersion 19
        }

        real {
        }
    }

    buildTypes {
        debug {
            zipAlignEnabled true
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project-debug.txt'
            signingConfig signingConfigs.debug
        }

        release {
            zipAlignEnabled true
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
//            signingConfig signingConfigs.release
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            storeFile file("release.keystore")
            storePassword "volunculture"
            keyAlias "volunculture.android"
            keyPassword "volunculture.android"
        }
    }

    packagingOptions {
        // Butter Knife
        exclude 'META-INF/services/javax.annotation.processing.Processor'

        // Jackson
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file

    }
}

android.applicationVariants.all { variant ->
    def df = new SimpleDateFormat("yyyyMMdd")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))

    def buildTime = df.format(new Date())
    def newApkName = 'vocketlist-' + android.defaultConfig.versionName + '-' + variant.buildType.name + "-" + buildTime

    variant.outputs.each { output ->
        def newApkPath = output.outputFile.parent
        def newAlignedApkPath = new File(newApkPath, 'aligned')
        output.outputFile = new File(newAlignedApkPath, newApkName + '.apk')
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.0'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

//    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    testCompile 'junit:junit:4.12'

    // 내부 라이브러리
    compile project(':libraries:logger')
    compile project(':libraries:network')
    compile project(':libraries:daummap')           // http://apis.map.daum.net/android/guide/
    compile project(':libraries:AwesomeText')     // hash tag widget

    // Glide : (Image Load Library)[https://github.com/bumptech/glide]
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Butter Knife : [https://github.com/JakeWharton/butterknife]
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // Firebase (https://firebase.google.com/)
    compile 'com.google.firebase:firebase-messaging:10.0.1' // Firebase message
    compile 'com.google.firebase:firebase-crash:10.0.1'     // Firebase App Crash Report
    compile 'com.google.firebase:firebase-core:10.0.1'      // Firebase Analytics

    // Share
    compile group: 'com.kakao.sdk', name: 'kakaolink', version: '1.1.0' // 카카오링크 (https://developers.kakao.com/)
    compile 'com.facebook.android:facebook-android-sdk:4.19.0'          // facebook (https://developers.facebook.com/docs/android/downloads?locale=ko_KR)

    // Chooser : https://github.com/coomar2841/android-multipicker-library
    compile 'com.kbeanie:multipicker:1.1.31@aar'
    // 텍스트 : https://github.com/binaryfork/Spanny
    compile 'com.binaryfork:spanny:1.0.4'

    // 매티리얼 다이얼로그 : https://github.com/afollestad/material-dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
    // Material color : https://github.com/mcginty/material-colors
    compile 'com.github.mcginty:material-colors:1.1.0'
    // 에디트 텍스트 : https://github.com/rengwuxian/MaterialEditText
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    // 링크 텍스트 : https://github.com/armcha/AutoLinkTextView
    compile 'com.github.armcha:AutoLinkTextView:0.3.0'

    // Circle Image : https://github.com/hdodenhof/CircleImageView
    compile 'de.hdodenhof:circleimageview:2.1.0'
    // Bottom Navigation : https://github.com/roughike/BottomBar
    compile 'com.roughike:bottom-bar:2.1.1'

    // Recycler View : https://github.com/Malinskiy/SuperRecyclerView
    compile 'com.malinskiy:superrecyclerview:1.1.4'
    // 확장 리스트 : https://github.com/bignerdranch/expandable-recycler-view
    compile 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'

    //badge
    compile 'me.leolin:ShortcutBadger:1.1.13@aar'

    //firebase-auth
    compile 'com.google.firebase:firebase-auth:10.0.1'
}

apply plugin: 'com.google.gms.google-services'